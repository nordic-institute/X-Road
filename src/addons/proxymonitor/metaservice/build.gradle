plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
}

project.ext.schemaTargetDir = new File("$buildDir/generated-sources")

sourceSets {
    main {
        java.srcDirs = ["src/main/java", "$buildDir/generated-sources"]
        resources.srcDirs = ['src/main/resources', '../../../common-util/src/main/resources']
    }
}

dependencies {
    //"shadow" includes also transitive dependencies
    implementation project(path: ':proxy', configuration: 'shadow')

    //workaround for Idea
    implementation project(':common-util')
    implementation project(":serverconf")
    implementation project(":common-op-monitoring")
    //workaround

    implementation project(':monitor-common')
    implementation project(':monitoring-conf')
    implementation project(':addons:proxymonitor:common')

    implementation "com.google.guava:guava:$guavaVersion"

    testImplementation project(path: ':common-util', configuration: 'testArtifacts')
    testImplementation project(path: ":proxy", configuration: 'testArtifacts')
    testImplementation project(':common-test')
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation "com.typesafe.akka:akka-testkit_$akkaVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
}

task createDirs() {
    doLast {
        project.ext.schemaTargetDir.mkdirs()
    }
}

task licenseFormatJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
}

task licenseTestJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
    check = true
}

licenseFormatMain.enabled = false
licenseMain.enabled = false

licenseFormat.dependsOn licenseFormatJava
licenseTest.dependsOn licenseTestJava

jar {
    enabled = false
}

shadowJar {
    archiveBaseName = "proxymonitor-metaservice"
    archiveClassifier = ''
    exclude('**/module-info.class')
    dependencies {
        include(project(':addons:proxymonitor:common'))
        include(project(':monitor-common'))
        include(project(':monitoring-conf'))
    }
}

build.dependsOn shadowJar
buildall.dependsOn shadowJar

compileJava.dependsOn processResources

task runProxymonitorMetaserviceTest(type: JavaExec) {

    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug',
                '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }

    jvmArgs '-Dxroad.proxy.ocspCachePath=build/ocsp-cache',
            '-Dxroad.tempFiles.path=build/attach-tmp',
            '-Dxroad.proxy.configurationFile=../../systemtest/conf/local_test/serverconf_producer.xml',
            '-Dxroad.proxy.jetty-serverproxy-configuration-file=src/test/resources/serverproxy.xml',
            '-Dxroad.proxy.jetty-clientproxy-configuration-file=src/test/resources/clientproxy.xml',
            '-Dlogback.configurationFile=src/test/resources/logback-metaservicetest.xml',
            '-Dxroad.proxy.jetty-ocsp-responder-configuration-file=src/test/resources/ocsp-responder.xml',
            '-Dxroad.proxy.client-connector-so-linger=-1',
            '-Dxroad.proxy.client-httpclient-so-linger=-1',
            '-Dxroad.proxy.server-connector-so-linger=-1',
            '-Dxroad.proxy.serverServiceHandlers=ee.ria.xroad.proxy.serverproxy.ProxyMonitorServiceHandlerImpl',
            '-Dproxy.akka.remote.artery.canonical.port=0'

    main = 'ee.ria.xroad.proxy.testsuite.ProxyTestSuite'
    classpath = sourceSets.test.runtimeClasspath
}
