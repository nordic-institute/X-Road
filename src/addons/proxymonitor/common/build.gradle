project.ext.schemaTargetDir = layout.buildDirectory.dir("generated-sources").get().asFile

configurations {
    xjc
}

sourceSets {
    main {
        java.srcDirs = ["src/main/java", schemaTargetDir]
    }
}

dependencies {
    implementation libs.jaxb.runtime

    xjc libs.bundles.jaxb
}

tasks.register('createDirs') {
    doLast {
        project.ext.schemaTargetDir.mkdirs()
    }
}

tasks.register('xjc') {
    inputs.files fileTree(dir: 'src/main/resources', include: '*.xsd')
    outputs.dir schemaTargetDir

    doLast {
        ant.taskdef(name: 'xjc',
            classname: 'com.sun.tools.xjc.XJCTask',
            classpath: configurations.xjc.asPath)

        // Generate classes for identifiers
        ant.xjc(
            destdir: project.ext.schemaTargetDir,
            package: 'ee.ria.xroad.proxymonitor.message',
            schema: 'src/main/resources/monitoring.xsd',
            binding: 'src/main/resources/jaxb-bindings.xml'
        )
    }

}

xjc.dependsOn createDirs
compileJava.dependsOn xjc
