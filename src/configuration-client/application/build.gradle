plugins {
    alias(libs.plugins.springBoot)
    id 'maven-publish'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            groupId = 'org.niis.xroad'
            artifactId = "configuration-client"
            version = xroadVersion + ("RELEASE".equals(xroadBuildType) ? "": "-SNAPSHOT")
        }
    }
    repositories {
        maven {
            url = xroadPublishUrl
            credentials {
                username = xroadPublishUser
                password = xroadPublishApiKey
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

dependencies {
    implementation(platform(libs.springCloud.bom))

    implementation project(':configuration-client:confclient-rpc-client')
    implementation project(':common:common-globalconf')
    implementation project(':common:common-scheduler')
    implementation project(':common:common-jetty')
    implementation project(':common:common-verifier')
    implementation project(':common:common-rpc')

    implementation('org.springframework:spring-context')
    implementation('org.springframework:spring-context-support')
    implementation libs.commons.cli

    testImplementation project(':common:common-test')
    testImplementation libs.wiremock.standalone
}

ext {
    mainClassName = 'org.niis.xroad.confclient.ConfClientDaemonMain'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveBaseName = 'configuration-client'
}

bootJar {
    enabled = true

    manifest {
        attributes(
            'Implementation-Title': 'X-Road GlobalConf client',
            'Implementation-Version': "$xroadVersion-$xroadBuildType",
            'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
        )
    }
}

//shadowJar {
//    exclude('**/module-info.class')
//    archiveClassifier = ''
//    archiveBaseName = 'configuration-client'
//    mergeServiceFiles()
//}

jar.enabled = false

//build {
//    dependsOn shadowJar
//}

tasks.register('runConfigurationClient', JavaExec) {
    jvmArgs '-Dxroad.conf.path=build/',
        '-Dxroad.distributedfiles.signatureFreshness=-1' // disable freshness checking since we read local file
    mainClass = mainClassName
    classpath = sourceSets.main.runtimeClasspath
}

