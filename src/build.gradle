buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0') {
            exclude group: 'org.springframework', module: 'spring-core'
        }
    }
}

plugins {
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '7.0.4.1'
    id 'jacoco'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id 'org.springframework.boot' version '2.3.12.RELEASE' apply false
}

def getRubySourceCheckCommand(def projectPath) {
    return ["$rootDir/check_ruby_source.sh", "$projectPath"]
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarqube.niis.org"
        property "sonar.projectKey", "xroad"
        property "sonar.projectName", "X-Road"
        property "sonar.projectDescription", "Data Exchange Layer"
        property "sonar.projectVersion", xroadVersion
        property "sonar.dependencyCheck.reportPath", "build/reports/owasp-dependency-check/dependency-check-report.xml"
        property "sonar.dependencyCheck.htmlReportPath", "build/reports/owasp-dependency-check/dependency-check-report.html"
        property "sonar.exclusions", "**/build/generated-sources/**"
    }
}

dependencyCheck {
    outputDirectory = "${project.buildDir}/reports/owasp-dependency-check"
    suppressionFile = "${project.rootDir}/dependencycheck-suppressions.xml"
    format = "ALL"
    analyzers {
        experimentalEnabled = false
        archiveEnabled = false
        jarEnabled = true
        centralEnabled = false
        bundleAuditEnabled = true
        nexusEnabled = false
        nuspecEnabled = false
        assemblyEnabled = false
        pyDistributionEnabled = false
        pyPackageEnabled = false
        rubygemsEnabled = false
        cmakeEnabled = false
        autoconfEnabled = false
        composerEnabled = false
        nodeEnabled = false
    }
}


allprojects {
    jacoco {
        toolVersion = "0.8.6"
    }

    task allDependencies(type: DependencyReportTask) {}

    task printDeps {
        doLast {
            if (configurations.hasProperty("runtime")) {
                configurations.runtime.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
                    println "${project.name}:${dep.module.id.group}:${dep.module.id.name}:${dep.module.id.version}"
                }
            }
        }
    }
}

subprojects {

    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    group = 'ee.ria.xroad'
    version = '1.0'

    repositories {
        mavenCentral()
        // XXX In case mavenCentral is unreachable, use this mirror instead
        // maven {
        //     url "http://mirrors.ibiblio.org/pub/mirrors/maven2"
        // }

        // iaikPkcs11Wrapper is located there
        //maven {
        //    url "http://ground.zero.ee/maven2"
        //}
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    dependencies {
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'org.hamcrest:hamcrest-library:2.2'
        testImplementation 'junit:junit:4.13.2'

        compileOnly 'org.projectlombok:lombok:1.18.18'
        annotationProcessor 'org.projectlombok:lombok:1.18.18'

        testCompileOnly 'org.projectlombok:lombok:1.18.18'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.18'
    }

    task testJar(type: Jar) {
        classifier 'test'
        from sourceSets.test.output
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    tasks.withType(JavaExec) {
        if (project.hasProperty("args")) {
            args = project.getProperty("args").tokenize()
        }
    }

    task buildall(dependsOn: [build, ':center-ui:warble', ':center-service:warble']) {
        description = "Builds all that can be built (even the slow tasks)"
    }

    testJar.enabled = false

    artifacts {
        testArtifacts testJar
    }

    checkstyle {
        toolVersion = "8.44"
        configDirectory = file("${project.rootDir}/config/checkstyle")
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        showViolations = false
    }

    checkstyleMain {
        source = fileTree('src/main/java')
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE.txt')
        include '**/*.java'
        include '**/*.rb'
        skipExistingHeaders = true
    }

    jar {
        from rootProject.file('LICENSE.txt')
    }
}

configure(subprojects.findAll { it.name.contains('center-') or it.name.contains('common-ui') }) {

    apply plugin: 'com.github.hierynomus.license'

    def sources = (fileTree('app') + fileTree('etc') + fileTree('config') + fileTree('test'))

    task licenseFormatUi(type: nl.javadude.gradle.plugins.license.License) {
        source = sources
    }

    task licenseFormatUiCheck(type: nl.javadude.gradle.plugins.license.License) {
        source = sources
        check = true;
    }

    licenseFormat.dependsOn licenseFormatUi
    licenseTest.dependsOn licenseFormatUiCheck
}

configure(subprojects.findAll({ it.name.contains('center-common') or it.name.contains('common-ui') })) {
    def licenseSourceAdds = fileTree('lib')

    task licenseFormatLib(type: nl.javadude.gradle.plugins.license.License) {
        source += licenseSourceAdds
    }

    task licenseFormatLibCheck(type: nl.javadude.gradle.plugins.license.License) {
        source += licenseSourceAdds
        check = true
    }

    licenseFormat.dependsOn licenseFormatLib
    licenseTest.dependsOn licenseFormatLibCheck

}

task clean(type: Delete) {
    delete "${rootDir}/packages/build"
}
