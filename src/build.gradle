buildscript {
    dependencies {
        classpath (libs.licenseGradlePlugin) {
            exclude group: 'org.springframework', module: 'spring-core'
        }
    }
}

plugins {
    alias(libs.plugins.sonarqube)
    id 'jacoco-report-aggregation'
    id 'java'
    id 'idea'
    alias(libs.plugins.springBoot) apply false
}

sonarqube {
    properties {
        property "sonar.host.url", "${sonarqubeHost}"
        property "sonar.projectKey", "${sonarqubeProjectKey}"
        property "sonar.organization", "${sonarqubeOrganization}"
        property "sonar.projectName", "X-Road"
        property "sonar.projectDescription", "Data Exchange Layer"
        property "sonar.projectVersion", xroadVersion
        property "sonar.exclusions", "**/build/generated-sources/**"
        property "sonar.coverage.jacoco.xmlReportPaths", "${rootProject.layout.buildDirectory.get().asFile}/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"

        property "sonar.issue.ignore.multicriteria", "e1"
        //# ignore 'Local-Variable Type Inference should be used"
        property "sonar.issue.ignore.multicriteria.e1.ruleKey", "java:S6212"
        property "sonar.issue.ignore.multicriteria.e1.resourceKey", "**/*.java"
    }
}

group = 'ee.ria.xroad'
subprojects {
    group = "${parent.group}.${name.replace(/\W/, "_")}"
}

allprojects {

    version = '1.0'

    tasks.register('allDependencies', DependencyReportTask) {}
}

//Configure java projects
configure(subprojects.findAll { !["shared-ui", "ui"].contains(it.name) }) {

    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    version = '1.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    sourceSets {
        intTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime

        intTestImplementation.extendsFrom implementation
        intTestRuntimeOnly.extendsFrom runtimeOnly
    }

    dependencies {
        testImplementation(libs.junit.jupiterEngine)
        testImplementation(libs.junit.vintageEngine)

        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        intTestCompileOnly libs.lombok
        intTestAnnotationProcessor libs.lombok
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('testJar', Jar) {
        archiveClassifier = 'test'
        from sourceSets.test.output
    }

    def javaCompiler = javaToolchains.compilerFor(java.toolchain)
    def javaHome = javaCompiler.get().metadata.installationPath.asFile.absolutePath

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs << '-parameters'
    }

    tasks.withType(JavaExec).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
        if (project.hasProperty("args")) {
            args = project.getProperty("args").tokenize()
        }
    }

    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }

    tasks.withType(Exec).configureEach {
        environment "JAVA_HOME", javaHome
        environment "JAVA_TOOL_OPTIONS", '-Dfile.encoding=UTF-8'
    }

    testJar.enabled = false

    artifacts {
        testArtifacts testJar
    }

    checkstyle {
        toolVersion = "10.12.6"
        configDirectory = file("${project.rootDir}/config/checkstyle")
        ignoreFailures = false
        showViolations = false
        enableExternalDtdLoad = true
    }

    checkstyleMain {
        source = fileTree('src/main/java')
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleTest {
        source = fileTree('src/test/java')
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle-test.xml")
    }

    configurations.checkstyle {
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
            select("com.google.guava:guava:0")
        }
    }

    jacoco {
        toolVersion = "0.8.11"
    }
    // Do not generate reports for individual projects
    tasks.named("jacocoTestReport") {
        enabled = false
    }

    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE.txt')
        include '**/*.java'
        skipExistingHeaders = true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    licenseMain.source = fileTree('src/main')
    licenseTest.source = fileTree('src/test')
    licenseFormatMain.source = fileTree('src/main')
    licenseFormatTest.source = fileTree('src/test')

    jar {
        from rootProject.file('LICENSE.txt')
        duplicatesStrategy = DuplicatesStrategy.WARN
    }
}

clean.delete << "${rootDir}/packages/build"

dependencies {
    subprojects {
        pluginManager.withPlugin('java') {
            jacocoAggregation project
        }
    }
}

reporting {
    reports {
        jacocoAggregatedReport(JacocoCoverageReport) {
            testType = 'full'
            reportTask.configure {
                description = 'Build a full test coverage report including test and integrationTest results'
                project.subprojects {
                    pluginManager.withPlugin('jacoco') {
                        executionData tasks.withType(Test)
                    }
                }
                reports {
                    xml.required = true
                }
            }
        }
    }
}

tasks.withType(Jar).configureEach {
    enabled = false
}

//Register git-hooks
tasks.register("installGitHooks", Copy) {
    from file("$rootDir/../.githooks")
    into file("$rootDir/../.git/hooks")
    fileMode 0775
}
assemble.dependsOn installGitHooks
tasks.sonar.dependsOn testCodeCoverageReport
