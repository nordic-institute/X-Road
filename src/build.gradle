buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1') {
            exclude group: 'org.springframework', module: 'spring-core'
        }
    }
}

plugins {
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'jacoco'
    id 'java'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
    id 'org.springframework.boot' version "${springBootVersion}" apply false
}

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarqube.niis.org"
        property "sonar.projectKey", "xroad"
        property "sonar.projectName", "X-Road"
        property "sonar.projectDescription", "Data Exchange Layer"
        property "sonar.projectVersion", xroadVersion
        property "sonar.dependencyCheck.reportPath", "build/reports/owasp-dependency-check/dependency-check-report.xml"
        property "sonar.dependencyCheck.htmlReportPath", "build/reports/owasp-dependency-check/dependency-check-report.html"
        property "sonar.exclusions", "**/build/generated-sources/**"
        property "sonar.coverage.jacoco.xmlReportPaths", "${rootProject.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

dependencyCheck {
    outputDirectory = "${project.buildDir}/reports/owasp-dependency-check"
    suppressionFile = "${project.rootDir}/dependencycheck-suppressions.xml"
    format = "ALL"
    analyzers {
        experimentalEnabled = false
        archiveEnabled = false
        jarEnabled = true
        centralEnabled = false
        bundleAuditEnabled = false
        nexusEnabled = false
        nuspecEnabled = false
        opensslEnabled = false
        msbuildEnabled = false
        assemblyEnabled = false
        pyDistributionEnabled = false
        pyPackageEnabled = false
        rubygemsEnabled = false
        cmakeEnabled = false
        autoconfEnabled = false
        composerEnabled = false
        nodeEnabled = false
        nodeAudit {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
    }
}


allprojects {
    group = 'org.niis.xroad'
    version = '1.0'
    
    buildscript {
      repositories {
        mavenCentral()
      }
    }

    repositories {
      mavenCentral()
      mavenLocal()
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    task allDependencies(type: DependencyReportTask) {}

    task printDeps {
        doLast {
            if (configurations.hasProperty("runtime")) {
                configurations.runtime.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
                    println "${project.name}:${dep.module.id.group}:${dep.module.id.name}:${dep.module.id.version}"
                }
            }
        }
    }
}

configure(subprojects.findAll { !["frontend","shared-ui","admin-ui"].contains(it.name) }) {

    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }

    tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    dependencies {
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'org.hamcrest:hamcrest-library:2.2'
        testImplementation "junit:junit:$junitVersion"

        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'

        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    }

    task testJar(type: Jar) {
        classifier 'test'
        from sourceSets.test.output
    }

    tasks.withType(JavaExec) {
        if (project.hasProperty("args")) {
            args = project.getProperty("args").tokenize()
        }
    }

    task buildall(dependsOn: [build]) {
        description = "Builds all that can be built (even the slow tasks)"
    }

    testJar.enabled = false

    artifacts {
        testArtifacts testJar
    }

    checkstyle {
        toolVersion = "8.44"
        configDirectory = file("${project.rootDir}/config/checkstyle")
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        showViolations = false
    }

    checkstyleMain {
        source = fileTree('src/main/java')
    }

    checkstyleTest {
        source = fileTree('src/test/java')
    }

    jacocoTestReport {
        enabled = false
    }

    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE.txt')
        include '**/*.java'
        skipExistingHeaders = true
    }

    jar {
        from rootProject.file('LICENSE.txt')
    }

}

clean.delete << "${rootDir}/packages/build"

jacocoTestReport {
    def javaProjects = subprojects.findAll{it.plugins.hasPlugin(JavaPlugin)}
    dependsOn javaProjects.check
    reports {
        xml.required = true
    }
    classDirectories.setFrom files(javaProjects.sourceSets.main.java.classesDirectory)
    sourceDirectories.setFrom files(javaProjects.sourceSets.main.java.srcDirs)
    executionData.setFrom fileTree(dir: '.', include: '**/build/jacoco/*.exec')
}
