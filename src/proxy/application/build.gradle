plugins {
    alias(libs.plugins.quarkus)
//    alias(libs.plugins.jandex)
}

dependencies {
    implementation(platform(libs.quarkus.bom))

    implementation(libs.quarkus.springBoot.di) //TODO might be removed , comes from bootstrap
    implementation(libs.quarkus.kubernetes) //TODO use classifer?

    implementation(project(':proxy:core'))
    implementation(project(':common:common-quarkus-bootstrap'))
    implementation(project(':common:common-core'))
    implementation(project(':common:common-rpc'))

    implementation(libs.apache.httpclient)
    implementation(project(':common:common-keyconf'))
    implementation(project(':common:common-messagelog'))
    implementation(project(':common:common-op-monitoring'))
    implementation(project(':serverconf'))
    implementation(project(':addons:metaservice'))
    implementation(project(':addons:messagelog:messagelog-addon:asic'))
    implementation(project(':addons:messagelog:messagelog-addon:core'))
    implementation(project(':addons:proxymonitor-metaservice'))
    implementation(project(':addons:op-monitoring'))

    testImplementation(libs.hsqldb)
    testImplementation(libs.restAssured)

    testImplementation(project(':common:common-domain'))
    testImplementation(project(':common:common-globalconf'))
    testImplementation(project(':common:common-jetty'))
    testImplementation(project(':common:common-message'))
    testImplementation(project(':common:common-test'))
    testImplementation(project(':common:common-verifier'))
    testImplementation(project(':security-server:ds-client'))
    testImplementation(project(path: ":proxy:core", configuration: 'testArtifacts'))

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

jar {
    enabled = false
}

quarkusDev {
    jvmArgs = ['-Xmx256m']
}

quarkusRun {
    jvmArgs = ['-Xmx130m',"-XX:MaxMetaspaceSize=100m"]
}

testJar.enabled = true

apply plugin: "jacoco"

test {
    useJUnit {
        excludeCategories 'ee.ria.xroad.proxy.testutil.IntegrationTest'
    }
    jacoco {
        destinationFile = file("build/jacoco/unitTest.exec")
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    shouldRunAfter test
    jacoco {
        destinationFile = file("build/jacoco/integrationTest.exec")
    }
    useJUnit {
        includeCategories 'ee.ria.xroad.proxy.testutil.IntegrationTest'
    }
    reports {
        junitXml.includeSystemOutLog = false // defaults to true
    }
}

tasks.register('runProxyTest', JavaExec) {
    group = "verification"
    shouldRunAfter integrationTest
    jvmArgs '-Xmx2g',
        "-Dxroad.proxy.ocspCachePath=build/ocsp-cache",
        "-Dxroad.tempFiles.path=build/attach-tmp",
        '-Dlogback.configurationFile=src/test/logback-proxytest.xml',
        '-Dxroad.proxy.grpc-tls-enabled=false'
//      '-Djava.security.properties==src/main/resources/java.security'

    mainClass = 'ee.ria.xroad.proxy.testsuite.ProxyTestSuite'
    classpath = sourceSets.test.runtimeClasspath
}

jacoco {
    applyTo runProxyTest
}

check.dependsOn integrationTest
