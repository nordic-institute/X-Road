plugins {
    id 'com.github.johnrengelman.shadow'
}

dependencies {
    implementation "commons-cli:commons-cli:$commonsCliVersion"
    implementation 'com.googlecode.clichemaven:cliche:110413'

    implementation project(':common:common-domain')
    implementation project(':signer-protocol')
}

ext {
    mainClassName = 'ee.ria.xroad.signer.console.SignerCLI'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveClassifier = ''
    exclude('**/module-info.class')
    exclude 'asg/cliche/example/**'
    from rootProject.file("LICENSE.txt")
    mergeServiceFiles()
}

jar.enabled = false

build {
    dependsOn shadowJar
}

tasks.register('runSignerConsole', JavaExec) {
    jvmArgs '-Djava.library.path=../passwordstore'
    mainClass = 'ee.ria.xroad.signer.console.SignerCLI'
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in;
}
