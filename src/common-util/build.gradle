project.ext.schemaTargetDir = new File("$buildDir/generated-sources")

configurations {
    xjc
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated-sources']
    }
}

dependencies {
    compile 'org.apache.james:apache-mime4j-core:0.8.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.santuario:xmlsec:2.0.8'

    compile 'org.bouncycastle:bcpkix-jdk15on:1.54'
    compile 'commons-configuration:commons-configuration:1.9'
    compile 'commons-io:commons-io:2.6'
    compile 'commons-jxpath:commons-jxpath:1.3'
    compile 'org.slf4j:jcl-over-slf4j:1.7.12'
    compile 'org.glassfish.jaxb:jaxb-runtime:2.2.11'
    compile 'xerces:xercesImpl:2.11.0.SP5'

    compile "org.eclipse.jetty:jetty-server:$jettyVersion"

    compile 'ch.qos.logback:logback-access:1.2.3'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile ('org.quartz-scheduler:quartz:2.1.6') {
        exclude module: 'c3p0'
    }

    compile 'joda-time:joda-time:2.1'
    compile 'xalan:serializer:2.7.2'

    compile 'org.apache.httpcomponents:httpclient:4.5.2'
    compile 'org.apache.httpcomponents:httpasyncclient:4.1.1'

    compile "com.typesafe.akka:akka-actor_2.11:$akkaVersion"
    compile "com.typesafe.akka:akka-remote_2.11:$akkaVersion"
    compile "com.typesafe.akka:akka-slf4j_2.11:$akkaVersion"

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.google.guava:guava:19.0'

    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile project(':common-test').sourceSets.main.output

    xjc "org.glassfish.jaxb:jaxb-xjc:$jaxbVersion"
    xjc "org.glassfish.jaxb:jaxb-runtime:$jaxbVersion"
}

test {
    jvmArgs '-Djava.library.path=../lib'
}

task createDirs() {
    doLast {
        project.ext.schemaTargetDir.mkdirs()
    }
}

task xjc() {
    inputs.files fileTree(dir: 'src/main/resources', include: '*.xsd')
    outputs.dir schemaTargetDir

    doLast {
    ant.taskdef(name: 'xjc',
        classname: 'com.sun.tools.xjc.XJCTask',
        classpath: configurations.xjc.asPath)

    // Generate classes for identifiers
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.identifier',
        schema: 'src/main/resources/identifiers.xsd'
    )

    // Generate classes for message, using identifier classes
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.message',
        schema: 'src/main/resources/message.xsd',
        binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for federateable global external conf
    ant.xjc(
            destdir: project.ext.schemaTargetDir,
            package: 'ee.ria.xroad.common.conf.globalconf.sharedparameters.v1',
            schema: 'src/main/resources/globalconf/external-conf.xsd',
            binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for federateable global internal conf
    ant.xjc(
            destdir: project.ext.schemaTargetDir,
            package: 'ee.ria.xroad.common.conf.globalconf.privateparameters.v1',
            schema: 'src/main/resources/globalconf/internal-conf.xsd',
            binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for federateable global external conf
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.conf.globalconf.sharedparameters.v2',
        schema: 'src/main/resources/globalconf/shared-parameters.xsd',
        binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for federateable global internal conf
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.conf.globalconf.privateparameters.v2',
        schema: 'src/main/resources/globalconf/private-parameters.xsd',
        binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for request, using identifier classes
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.request',
        schema: 'src/main/resources/request.xsd',
        binding: 'src/main/resources/identifiers-bindings.xml'
    )

    // Generate classes for hash chains
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.hashchain',
        schema: 'src/main/resources/hashchain.xsd',
    )

    // Generate classes for service metainfo
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.ria.xroad.common.metadata',
        schema: 'src/main/resources/service-metainfo.xsd',
        binding: 'src/main/resources/identifiers-bindings.xml'
    )
    }
}

task make(type: Exec) {
    inputs.dir fileTree(dir: '../passwordstore', includes: ['*.c','*.h','Makefile'])
    outputs.dir file('../lib')

    workingDir '../'
    commandLine 'make'
}

task makeClean(type: Exec) {
    workingDir '../'
    commandLine 'make', 'clean'
}

licenseFormatTest {
    outputs.upToDateWhen { false }
}

classes.dependsOn make
clean.dependsOn makeClean

xjc.dependsOn createDirs
compileJava.dependsOn xjc

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({ d, f -> f != 'generated-sources' } as FilenameFilter).each { excludeDirs += it }
}

task licenseFormatJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
}

task licenseTestJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
    check = true
}

licenseFormatMain.enabled = false
licenseMain.enabled = false

licenseFormat.dependsOn licenseFormatJava
licenseTest.dependsOn licenseTestJava

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({ d, f -> f != 'generated-sources' } as FilenameFilter).each { excludeDirs += it }
}
