import org.siouan.frontendgradleplugin.infrastructure.gradle.RunPnpmTaskType

plugins {
    alias(libs.plugins.frontendJDK21)
}

frontend {
    nodeVersion = frontendNodeVersion
    nodeInstallDirectory = file("${rootDir}/.gradle/pnpm-node/${frontendNodeVersion}")
    cacheDirectory = file("${projectDir}/.gradle/pnpm-cache")
    nodeDistributionUrlRoot = 'https://artifactory.niis.org/artifactory/nodejs-dist-remote/'
    maxDownloadAttempts = 3
    corepackVersion = "0.31.0"

    packageJsonDirectory = file("${rootDir}/")
    if (System.getenv().containsKey("CI")) {
        installScript = 'install --frozen-lockfile'
    }
}

tasks.register('build-pnpm-workspace', RunPnpmTaskType) {
    dependsOn installFrontend
    args = 'run build-workspace'
}

tasks.named("cleanFrontend").configure {
    enabled(false)
}

tasks.register('checkFrontAudit', RunPnpmTaskType) {
    dependsOn assembleFrontend

    args = 'run npx-check-audit'
}

tasks.register('test', RunPnpmTaskType) {
    dependsOn installFrontend
    args = 'run test-ss'
}

if (project.hasProperty('frontend-npm-audit')) {
    assemble.dependsOn checkFrontAudit
}

//check that pnpm run license-check passes
tasks.register('checkFrontWorkspaceLicense', RunPnpmTaskType) {
    dependsOn installFrontend
    args = ' run -r license-check'
}
checkFrontend.dependsOn checkFrontWorkspaceLicense

clean {
    delete file('dist')
}
