plugins {
    id("xroad.java-conventions")
    id("xroad.int-test-conventions")
}

configurations {
    dist {
        canBeConsumed = false
        canBeResolved = true
    }
    liquibaseLibs {
        apply plugin: 'base'
    }
}

dependencies {
    intTestImplementation project(path: ":central-server:admin-service:infra-jpa", configuration: "changelogJar")
    intTestImplementation project(":central-server:openapi-model")
    intTestImplementation project(":common:common-core")

    intTestImplementation(libs.bundles.testAutomation)
    intTestImplementation(libs.testAutomation.assert)
    intTestImplementation(libs.liquibase.core)
    intTestImplementation(libs.postgresql)
    intTestImplementation(libs.lombok)
}

test {
    useJUnitPlatform()
}

tasks.register('intTest', Test) {
    useJUnitPlatform()
    dependsOn(':central-server:admin-service:application:bootJar')
    setDescription("Runs integration tests.")
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    def intTestArgs = []
    if (project.hasProperty('intTestTags')) {
        intTestArgs += "-Dtest-automation.cucumber.filter.tags=" + project.getProperty('intTestTags')
    }
    if (project.hasProperty('intTestProfilesInclude')) {
        intTestArgs += "-Dspring.profiles.include=" + project.getProperty('intTestProfilesInclude')
    }

    jvmArgs intTestArgs

    testLogging {
        showStackTraces(true)
        showExceptions(true)
        showCauses(true)
        showStandardStreams(true)
    }
    reports {
        junitXml.includeSystemOutLog = false // defaults to true
    }

    shouldRunAfter test
}

tasks.named('check') {
    dependsOn tasks.named('intTest')
}

//archUnit {
//    skip = true
//}
