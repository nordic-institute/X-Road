dependencies {
    intTestImplementation project(":central-server:openapi-model")
    intTestImplementation project(":common:common-int-test")
    intTestImplementation(libs.bundles.testAutomation)
    intTestImplementation(libs.testAutomation.selenide) {
        exclude group: "org.slf4j", module: "*"
    }
    intTestImplementation(libs.bouncyCastle.bcpkix)
    intTestImplementation(libs.awaitility)
}

task systemTest(type: Test) {
    useJUnitPlatform()

    setDescription("Runs integration tests.")
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    def systemTestArgs = []
    if (project.hasProperty('systemTestTags')) {
        systemTestArgs += "-Dtest-automation.cucumber.filter.tags=" + project.getProperty('systemTestTags')
    }
    if (project.hasProperty('systemTestServeReport')) {
        systemTestArgs += "-Dtest-automation.report.allure.serve-report.enabled=" + project.getProperty('systemTestServeReport')
    }
    if (project.hasProperty('systemTestCentralServerUrl')) {
        systemTestArgs += "-Dtest-automation.custom.central-server-url-override=" + project.getProperty('systemTestCentralServerUrl')
    }
    if (project.hasProperty('systemTestCsImageName')) {
        systemTestArgs += "-Dtest-automation.custom.image-name=" + project.getProperty('systemTestCsImageName')
    }

    jvmArgs systemTestArgs

    testLogging {
        showStackTraces(true)
        showExceptions(true)
        showCauses(true)
        showStandardStreams(true)
    }
}
