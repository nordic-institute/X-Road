buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'base'
    id 'org.siouan.frontend-jdk17' version "$frontendVersion"
}

configurations {
    npm
    dist
}

dependencies {
  npm project(path: ":shared-ui-3", configuration: "npm")
}

frontend {
  var nodeDir = file("${rootDir}/.gradle/yarn-node/${frontendNodeVersion}")
  nodeVersion = frontendNodeVersion
  nodeInstallDirectory = nodeDir
  cacheDirectory = file("${projectDir}/.gradle/yarn-cache")
  if(nodeDir.exists()){
    nodeDistributionProvided = true
  }


  if (System.getenv().containsKey("CI")) {
    installScript='install --immutable'
  }
}

import org.siouan.frontendgradleplugin.infrastructure.gradle.RunYarn

task yarnUpdateDeps(type: RunYarn, dependsOn: [configurations.npm, installPackageManager]) {
  var currentDir = java.nio.file.Path.of("${projectDir}")
  inputs.files('package.json')
  outputs.files('yarn.lock')

  var packages = configurations.npm.files
    .collect { it.toPath() }
    .collect { currentDir.relativize(it) }
    .collect { '@niis/shared-ui@file:' + it }
    .join(' ')

  script = 'add  ' + packages
}

installFrontend {
  dependsOn += 'yarnUpdateDeps'
}

task yarnGenerateTypes(type: RunYarn, dependsOn: installFrontend) {
  inputs.files('./package.json',
    '../infra-api-rest/src/main/resources/openapi-definition.yaml')
  outputs.dir('src/openapi-types')
  outputs.dir("${buildDir}/openapi")
  script = 'run generate-types'

  doFirst {
    project.delete('src/openapi')
  }
}

task yarnBuild(type: RunYarn, dependsOn: yarnGenerateTypes) {
  inputs.files(
    'src/',
    'public/',
    'package.json',
    'yarn.lock',
    'vite.config.js',
    'tsconfig.js',
    fileTree('node_modules') {
      exclude '.cache'
      exclude 'webpack-dev-server/ssl'
    }
  )
  outputs.dir('dist')
  script = 'run build:gradle'
}

task checkFrontAudit(type: RunYarn, dependsOn: installFrontend) {
  inputs.files(
    'package.json',
    'yarn.lock'
  )
  outputs.files("${buildDir}/reports/audit-ci.txt")
  script = 'run npx-check-audit'
}

// check that npm run license-check passes
task checkFrontLicense(type: RunYarn, dependsOn: installFrontend) {
  inputs.files(
    "test/",
    "public/",
    fileTree('src/') {
      exclude 'openapi-types'
    }
  )
  outputs.upToDateWhen { true }
  script = 'run license-check'
}

clean {
  delete file('dist')
}

assemble.dependsOn yarnBuild
check.dependsOn checkFrontLicense

if (project.hasProperty('frontend-npm-audit')) {
  yarnBuild.dependsOn checkFrontAudit
}

artifacts {
  dist(file("dist/")) {
    builtBy(yarnBuild)
  }
}
