plugins {
    id 'io.spring.dependency-management'
    id 'org.springframework.boot'
    id 'org.openapi.generator' version "$openApiGeneratorVersion"
}

bootJar {
    manifest {
        attributes(
            'Implementation-Title': 'xroad-centralserver-registration-service',
            'Implementation-Version': xroadVersion
        )
    }
}

sourceSets {
    main {
        java.srcDirs = [
            'src/main/java',
            "${buildDir}/generated-sources/openapi/src/main/java",
        ]
    }
}

archivesBaseName = 'centralserver-registration-service'

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${projectDir}/../admin-service/src/main/resources/openapi-definition.yaml"
    outputDir = "${buildDir}/generated-sources/openapi"
    modelPackage = "org.niis.xroad.centralserver.registrationservice.openapi.model"
    globalProperties = [
        apiDocs                : "false",
        apiTests               : "false",
        modelDocs              : "false",
        modelTests             : "false",
        models                 : "",
        apis                   : "false",
        generateSupportingFiles: "false"
    ]
    configOptions = [
        openApiNullable: "false",
        "swaggerAnnotations": "false"
    ]
}

// Fix up to date check
tasks.openApiGenerate {
    inputs.files(openApiGenerate.inputSpec)
    outputs.dir(openApiGenerate.outputDir)
}

compileJava.dependsOn tasks.openApiGenerate

dependencies {
    annotationProcessor "org.springframework:spring-context-indexer"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation(project(':common-util')) {
        exclude group: 'com.typesafe.akka'
        exclude group: 'org.eclipse.jetty'
        exclude group: 'org.quartz-scheduler'
    }

    implementation(project(':common-verifier')) {
        transitive=false
    }

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-jetty')

    implementation('io.swagger.core.v3:swagger-annotations:2.2.0')
    implementation('javax.validation:validation-api')
    implementation('com.github.vladimir-bukhtoyarov:bucket4j-core:7.4.0')

    testImplementation "junit:junit:$junitVersion"
    testImplementation project(':common-test')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation ('com.github.tomakehurst:wiremock-jre8:2.33.1')

}

