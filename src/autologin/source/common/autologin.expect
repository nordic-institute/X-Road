#!/usr/bin/expect
set XROAD_UTIL_PATH /usr/share/xroad/autologin
set CUSTOM_SCRIPT $XROAD_UTIL_PATH/custom-fetch-pin.sh
set DEFAULT_SCRIPT $XROAD_UTIL_PATH/default-fetch-pin.sh

proc log_in_to_token {token_id pin} {
    global XROAD_UTIL_PATH
    set result 0

    puts "Attempting to log in to token $token_id"

    spawn $XROAD_UTIL_PATH/signer-console login-token $token_id
    expect {
        "PIN:"  {
            send "$pin\r"
            exp_continue
        } "Signer.TokenNotAvailable" {
            set result 1
        } "HttpError" {
            set result 1
        } timeout {
            set result 1
        } "Signer.PinIncorrect" {
            set result 127
        } eof {
            ;
        }
    }

    if {$result == 127} {
        puts stderr "\nFATAL: Incorrect PIN for token $token_id"
    } elseif {$result == 1} {
        puts stderr "WARNING: Failed to login to token $token_id (token not available or connection error)"
    } else {
        puts "Successfully logged into token $token_id"
    }

    return $result
}

if {[file exists $CUSTOM_SCRIPT]} {
    set runcmd [list exec $CUSTOM_SCRIPT 2>@stderr]
} elseif {[file exists $DEFAULT_SCRIPT]} {
    set runcmd [list exec $DEFAULT_SCRIPT 2>@stderr]
} else {
    error "Failed to locate custom-fetch-pin.sh or default-fetch-pin.sh";
}

if {[catch $runcmd res]} {
  set ecode [lindex $::errorCode 2]
  if {$ecode eq 127} {
    puts "PIN not available, will not attempt to enter PIN code"
    exit 127
  } else {
    error "Failed to run command $runcmd - exit code $ecode"
  }
}

set pin_output $res
set result 0

# Check if we have multiple tokens or one
set lines [split $pin_output "\n"]
set line_count 0
foreach line $lines {
    if {[string length [string trim $line]] > 0} {
        incr line_count
        if {$line_count >= 2} {
            break
        }
    }
}

if {$line_count == 1} {
    set pin $pin_output
    set result [log_in_to_token 0 $pin]
} else {
    foreach line $lines {
        if {[string length [string trim $line]] == 0} {
            continue
        }

        # Expect the line to be in the format [token-id]:[token-pin]
        set colon_pos [string first ":" $line]
        if {$colon_pos == -1} {
            puts stderr "\nFATAL: Invalid format in multi-token mode. Expected 'token-id:token-pin', got: $line"
            exit 127
        }

        set token_id [string trim [string range $line 0 [expr {$colon_pos - 1}]]]
        set pin [string range $line [expr {$colon_pos + 1}] end]

        set token_result [log_in_to_token $token_id $pin]

        # Always return the code of the most severe error that has occurred
        if {$token_result > $result} {
            set result $token_result
        }
    }
}

exit $result

