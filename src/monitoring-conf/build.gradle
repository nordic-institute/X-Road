project.ext.schemaTargetDir = layout.buildDirectory.dir("generated-sources").get().asFile

configurations {
    xjc
}

sourceSets {
    main {
        java.srcDirs = ["src/main/java", schemaTargetDir]
        resources.srcDirs = ['src/main/resources', '../common/common-domain/src/main/resources']
        resources.exclude 'version.properties'
    }
}

dependencies {
    implementation project(':common:common-domain')
    implementation project(':common:common-verifier')

    xjc libs.bundles.jaxb
}

tasks.register('createDirs') {
    doLast {
        project.ext.schemaTargetDir.mkdirs()
    }
}

tasks.register('xjc') {
    inputs.files fileTree(dir: 'src/main/resources', include: '*.xsd')
    outputs.dir schemaTargetDir

    doLast {
        ant.taskdef(name: 'xjc',
            classname: 'com.sun.tools.xjc.XJCTask',
            classpath: configurations.xjc.asPath)
        ant.xjc(
            destdir: project.ext.schemaTargetDir,
            package: 'ee.ria.xroad.common.conf.globalconf.monitoringparameters',
            schema: "${layout.buildDirectory.get().asFile}/resources/main/monitoring-conf.xsd",
            binding: "${layout.buildDirectory.get().asFile}/resources/main/identifiers-bindings.xml"
        )
    }
}

xjc.dependsOn createDirs
xjc.dependsOn processResources
xjc.mustRunAfter processResources

compileJava.dependsOn xjc
compileJava.dependsOn processResources
