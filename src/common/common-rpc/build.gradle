plugins {
    id 'java-library'
    id 'com.google.protobuf'
}

sourceSets {
    main {
        java.srcDirs = [
            'src/main/java'
            , 'build/generated-sources'
            , 'build/generated/source/proto/main/grpc'
            , 'build/generated/source/proto/main/java']
    }
}

dependencies {
    implementation project(':common:common-util')
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    api "io.grpc:grpc-netty-shaded:${grpcVersion}"
    api "com.google.protobuf:protobuf-java-util:${protocVersion}"
    api "jakarta.annotation:jakarta.annotation-api:1.3.5"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protocVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

compileJava.dependsOn generateProto
