plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
    id 'org.springframework.boot'
    id 'org.openapi.generator' version "$openApiGeneratorVersion"
    id 'org.niis.xroad.oasvalidatorplugin'
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', "build/generated-sources/openapi/src/main/java"]
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api project(':common:common-domain')
    implementation project(':common:common-util')
    implementation project(':signer-protocol')
    implementation project(':serverconf')

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation('io.micrometer:micrometer-tracing-bridge-brave')
    implementation('org.kohsuke:libpam4j:1.11')
    implementation("io.swagger.parser.v3:swagger-parser-v3:$swaggerParserVersion")
    implementation("io.swagger.core.v3:swagger-annotations:$swaggerAnnotationsVersion")

    testImplementation project(':common:common-test')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation("org.hsqldb:hsqldb:$hsqldbVersion")

    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
    implementation('jakarta.validation:jakarta.validation-api')
    implementation('org.apache.tika:tika-core:2.8.0')

    api("com.github.ben-manes.caffeine:caffeine")
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/common-openapi-definition.yaml".toString()
    outputDir = "$buildDir/generated-sources/openapi".toString()
    apiPackage = "org.niis.xroad.restapi.openapi"
    modelPackage = "org.niis.xroad.restapi.openapi.model"
    globalProperties = [
        modelDocs              : "false",
        apis                   : "",
        models                 : "", // must use empty strings instead of "true":
        // https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
        generateSupportingFiles: "false"
    ]
    configOptions = [
        useJakartaEe         : "true",
        interfaceOnly        : "true",
        useTags              : "true",
        documentationProvider: "none",
        skipDefaultInterface : "true",
        openApiNullable      : "false"
    ]
}

// Fix up to date check
tasks.openApiGenerate {
    inputs.files(openApiGenerate.inputSpec)
    outputs.dir(openApiGenerate.outputDir)
}

compileJava.dependsOn tasks.openApiGenerate

// set license format to ignore generated java sources
task licenseFormatJava(type: nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
}

task licenseTestJava(type: nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
    check = true
}

licenseFormatMain.enabled = false
licenseMain.enabled = false

licenseFormat.dependsOn licenseFormatJava
licenseTest.dependsOn licenseTestJava

apiValidationParameters.apiDefinitionPaths = [
    "$projectDir/src/main/resources/common-openapi-definition.yaml".toString()
]

validateApiDefinitions {
    inputs.files("src/main/resources/common-openapi-definition.yaml")
    outputs.upToDateWhen { true }
}

tasks.openApiGenerate.dependsOn 'validateApiDefinitions'
