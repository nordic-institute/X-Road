plugins {
    id 'java-library'
    alias(libs.plugins.openapi.generator)
    id 'org.niis.xroad.oasvalidatorplugin'
}

dependencies {
    compileOnly(libs.springBoot.starterWeb)
    compileOnly(libs.swagger.parserV3)

    api(libs.swagger.annotations)
    api(libs.jakarta.validationApi)
}

sourceSets {
    main {
        java.srcDirs = [
            'src/main/java',
            layout.buildDirectory.dir("generated-sources/openapi/src/main/java"),
        ]
    }
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/META-INF/openapi-definition.yaml"
    outputDir = "${layout.buildDirectory.get().asFile}/generated-sources/openapi"
    apiPackage = "org.niis.xroad.securityserver.restapi.openapi"
    modelPackage = "org.niis.xroad.securityserver.restapi.openapi.model"
    globalProperties = [
        modelDocs              : "false",
        apis                   : "", // must use empty strings instead of "true":
        // https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
        models                 : "",
        generateSupportingFiles: "true",
        supportingFiles        : 'ApiUtil.java'
    ]
    configOptions = [
        useJakartaEe         : "true",
        interfaceOnly        : "true",
        useTags              : "true",
        documentationProvider: "none",
        skipDefaultInterface : "true",
        openApiNullable      : "false"
    ]
}

// Fix up to date check
tasks.openApiGenerate {
    inputs.files(openApiGenerate.inputSpec)
    outputs.dir(openApiGenerate.outputDir)
}

apiValidationParameters.apiDefinitionPaths = [
    "$projectDir/src/main/resources/META-INF/openapi-definition.yaml".toString(),
    "$projectDir/src/main/resources/META-INF/openapi-authentication.yaml".toString()
]

validateApiDefinitions {
    inputs.files(
        "src/main/resources/META-INF/openapi-definition.yaml",
        "src/main/resources/META-INF/openapi-authentication.yaml")
    outputs.upToDateWhen { true }
}

tasks.openApiGenerate.dependsOn 'validateApiDefinitions'
tasks.compileJava.dependsOn tasks.openApiGenerate
