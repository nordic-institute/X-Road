import org.siouan.frontendgradleplugin.infrastructure.gradle.RunYarn

configurations {
    npm
    dist
}

dependencies {
    npm project(path: ":shared-ui", configuration: "npm")
}

tasks.register('yarnBuild', RunYarn) {
  dependsOn assembleFrontend
  inputs.files(
    'src/',
    'public/',
    'package.json',
    'yarn.lock',
    'tsconfig.js',
    'vite.config.js',
    fileTree('node_modules') {
      exclude '.cache'
      exclude 'webpack-dev-server/ssl'
    }
  )
  outputs.dir('dist')
  script = 'run build'
}

tasks.register('frontendUnitTests', RunYarn) {
  dependsOn assembleFrontend
  inputs.files("dist", "test", "public")
  outputs.upToDateWhen { true }
  script = 'run test:unit'
}

clean {
    delete file('dist')
}

// checkFrontLicense -> buildFront -> frontendUnitTests -> processResources
assemble.dependsOn yarnBuild

artifacts {
    dist(file("dist/")) {
        builtBy(yarnBuild)
    }
}
