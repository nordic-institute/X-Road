import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id("java")
    id("application")
    id("com.github.johnrengelman.shadow")
}

configurations {
    sqlDependencies

    configureEach {
        exclude group: 'org.eclipse.edc', module: 'jetty-core'
        exclude group: 'org.eclipse.edc', module: 'token-core'
    }

}

dependencies {
    implementation("org.eclipse.jetty.websocket:websocket-jakarta-server:$edcJettyVersion")

    implementation("org.eclipse.edc:control-plane-api-client:$edcVersion")
    implementation("org.eclipse.edc:control-plane-api:$edcVersion")
    implementation("org.eclipse.edc:control-plane-core:$edcVersion")
    implementation("org.eclipse.edc:dsp:$edcVersion")
    implementation("org.eclipse.edc:configuration-filesystem:$edcVersion")
    implementation("org.eclipse.edc:vault-filesystem:$edcVersion")
    implementation("org.eclipse.edc:management-api:$edcVersion")
    implementation("org.eclipse.edc:transfer-data-plane:$edcVersion")
    implementation("org.eclipse.edc:transfer-pull-http-receiver:$edcVersion")

    implementation("org.eclipse.edc:data-plane-selector-api:$edcVersion")
    implementation("org.eclipse.edc:data-plane-selector-core:$edcVersion")
//    implementation("org.eclipse.edc:data-plane-selector-client:$edcVersion")

    implementation("org.eclipse.edc:data-plane-control-api:$edcVersion")
    implementation("org.eclipse.edc:data-plane-public-api:$edcVersion")
    implementation("org.eclipse.edc:data-plane-core:$edcVersion")
    implementation("org.eclipse.edc:data-plane-http:$edcVersion")

//    implementation("org.eclipse.edc:iam-mock:$edcVersion")
    implementation("org.eclipse.edc:identity-trust-core:$edcVersion")
    implementation("org.eclipse.edc:identity-did-core:$edcVersion")
    implementation("org.eclipse.edc:identity-did-web:$edcVersion")
    implementation("org.eclipse.edc:identity-trust-issuers-configuration:$edcVersion")

    // embedded Identity Hub
    implementation("org.eclipse.edc:identity-hub-api:$edcVersion")
    implementation("org.eclipse.edc:identity-hub-credentials:$edcVersion")
    implementation("org.eclipse.edc:identity-hub-participants:$edcVersion")
    implementation("org.eclipse.edc:identity-hub-keypairs:$edcVersion")
    implementation("org.eclipse.edc:identity-hub-did:$edcVersion")

    // sql store
    sqlDependencies("org.postgresql:postgresql:$postgresqlVersion")
    sqlDependencies("org.eclipse.edc:sql-pool-apache-commons:$edcVersion")
    // without 'transaction-local' db connections are not returned to the pool and eventually edc stops working.
    sqlDependencies("org.eclipse.edc:transaction-local:$edcVersion")
    sqlDependencies("org.eclipse.edc:asset-index-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:data-plane-instance-store-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:data-plane-store-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:contract-definition-store-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:contract-negotiation-store-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:policy-definition-store-sql:$edcVersion")
    sqlDependencies("org.eclipse.edc:transfer-process-store-sql:$edcVersion")

    // x-road extensions
    implementation(project(":security-server:edc:extensions:xroad-messagelog")) {
        exclude group: 'org.eclipse.jetty'
    }
    implementation(project(":security-server:edc:extensions:xroad-payload-signer")) {
        exclude group: 'org.eclipse.jetty'
    }
    implementation(project(":security-server:edc:extensions:xroad-identity-and-trust"))

    testImplementation("org.eclipse.edc:junit:$edcVersion")
    testImplementation(project(':common:common-test')) {
        exclude group: 'org.eclipse.jetty'
    }
}

application {
    mainClass.set("org.eclipse.edc.boot.system.runtime.BaseRuntime")
}


var distTar = tasks.named("distTar")
var distZip = tasks.named("distZip")

shadowJar {
    mergeServiceFiles()
    archiveFileName.set("connector-inmemory.jar")
    dependsOn(distTar, distZip)
}

tasks.register('shadowJarWithSql', ShadowJar) {
    configurations = [project.configurations.runtimeClasspath, project.configurations.sqlDependencies]
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
    archiveFileName.set("connector.jar")
    manifest {
        attributes 'Main-Class': 'org.eclipse.edc.boot.system.runtime.BaseRuntime'
    }
    mergeServiceFiles()
    dependsOn(distTar, distZip)
}

assemble.dependsOn("shadowJarWithSql")
