import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id("java")
    id("application")
    alias(libs.plugins.shadow)
}

configurations {
    sqlDependencies

    configureEach {
        exclude group: 'org.eclipse.edc', module: 'jetty-core'
        exclude group: 'com.apicatalog', module: 'titanium-json-ld-jre8' //transient dep that creates errors
    }
}

dependencies {
    runtimeOnly(libs.bundles.connector)
    runtimeOnly(libs.edc.config.filesystem)
    runtimeOnly(libs.edc.ext.observability)
    runtimeOnly(libs.edc.dataplane.core)
    runtimeOnly(libs.edc.dataplane.api.control.config)
    runtimeOnly(libs.edc.dataplane.api.control.client)
    //runtimeOnly(libs.edc.dataplane.selfregistration) // currently we register dataplane instance manually
    runtimeOnly(libs.edc.dataplane.http)
    runtimeOnly(libs.edc.dataplane.iam)
    //runtimeOnly(libs.edc.dataplane.api.public)
    runtimeOnly(libs.edc.dataplane.api.signaling)
    runtimeOnly(libs.edc.ext.jsonld) // needed by the DataPlaneSignalingApi
    runtimeOnly(libs.edc.dpf.selector.client) // for the selector service -> self registration
    runtimeOnly(libs.edc.vault.hashicorp)

    sqlDependencies(libs.bundles.sql.edc.dataplane)

    implementation libs.bundles.edc.monitoring
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.38.0'

    // x-road extensions
    implementation(project(":security-server:edc:extensions:common:xroad-conf"))
    implementation(project(":security-server:edc:extensions:common:xroad-jsonld"))
    implementation(project(":security-server:edc:extensions:data-plane:xroad-messagelog")) {
        exclude group: 'org.eclipse.jetty'
    }
    implementation(project(":security-server:edc:extensions:data-plane:xroad-data-plane-public-api")) {
        exclude group: 'org.eclipse.jetty'
    }
    implementation(project(":security-server:edc:extensions:common:xroad-webserver"))

    implementation libs.apache.commonsLang3

    testImplementation libs.edc.junit
    testImplementation libs.mockito.jupiter
}

application {
    mainClass.set("org.eclipse.edc.boot.system.runtime.BaseRuntime")
}


var distTar = tasks.named("distTar")
var distZip = tasks.named("distZip")

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
        }
    }
}

shadowJar {
    mergeServiceFiles()
    archiveFileName.set("edc-data-plane-inmemory.jar")
    dependsOn(distTar, distZip)
}

tasks.register('shadowJarWithSql', ShadowJar) {
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath, project.configurations.sqlDependencies]
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
    archiveFileName.set("edc-data-plane.jar")
    manifest {
        attributes 'Main-Class': 'org.eclipse.edc.boot.system.runtime.BaseRuntime'
    }
    mergeServiceFiles()
    dependsOn(distTar, distZip)
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

assemble.dependsOn("shadowJarWithSql")
